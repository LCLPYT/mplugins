import work.lclpnet.build.task.GithubDeploymentTask

import java.nio.file.Files

plugins {
    id 'java'
	id 'fabric-loom' version '1.2.7'
	id 'maven-publish'
	id 'gradle-build-utils' version '1.5.3'
}

Properties props = buildUtils.loadProperties('publish.properties')

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = buildUtils.gitVersion()
group = project.maven_group

sourceSets {
    testPlugin {
        java {
            compileClasspath += main.output + main.compileClasspath
            runtimeClasspath += main.output + main.runtimeClasspath
        }
    }
}

configurations {
    testPluginImplementation.extendsFrom(implementation)
    testPluginRuntimeOnly.extendsFrom(runtimeOnly)
}

repositories {
    mavenCentral()

    maven {
        url "https://repo.lclpnet.work/repository/internal"
    }
}

loom {
    splitEnvironmentSourceSets()

    mods {
        mplugins {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    /* other Java dependencies */
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    'org.json:json:20230227'.with {
        implementation it
        include it
    }

    "work.lclpnet:json-config4j:${project.config_version}".with {
        implementation it
        include it
    }

    // make plugins4j visible for consumers at compile time
    "work.lclpnet:plugins4j:${project.plugins4j_version}".with {
        api it
        include it
    }

    'com.google.dagger:dagger:2.46.1'.with {
        implementation it
        include it
    }
    annotationProcessor 'com.google.dagger:dagger-compiler:2.46.1'
    include 'javax.inject:javax.inject:1'

    /* testing */
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.slf4j:slf4j-api:1.7.25'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

processTestPluginResources {
    inputs.property "version", project.version

    filesMatching("plugin.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
	it.options.release.convention(17)
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	withSourcesJar()
}

test {
    useJUnitPlatform()
}

[jar, sourcesJar].each {
    it.from(rootProject.file('LICENSE')) {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

tasks.register('testPluginJar', Jar) {
    from sourceSets.testPlugin.output
    archiveVersion.set(project.version.toString())
    archiveBaseName.set('testPlugin')
}

artifacts {
    archives testPluginJar
}

abstract class DeployPluginsTask extends DefaultTask {
    @TaskAction
    def execute() {
        final def proj = this.getProject()
        final def plugins = proj.getTasks().named('testPluginJar').get().outputs.files.files
        final def pluginsDir = proj.getProjectDir().toPath().resolve('run/plugins')

        copyPlugins(plugins, pluginsDir)

        println('Plugins have been deployed to %s'.formatted(pluginsDir))
    }

    static def copyPlugins(Set<File> plugins, java.nio.file.Path pluginsDir) {
        if (!Files.exists(pluginsDir)) Files.createDirectories(pluginsDir)

        plugins.forEach(plugin -> {
            copy(plugin.toPath(), pluginsDir.resolve(plugin.getName()))
        })
    }

    static def copy(java.nio.file.Path a, java.nio.file.Path b) {
        // transfer file contents to enable plugin hot-swapping using reloading
        try (def input = new FileInputStream(a.toFile()); def output = new FileOutputStream(b.toFile())) {
            input.transferTo output
        }
    }
}

tasks.register('deploy', DeployPluginsTask)

deploy.configure {
    dependsOn build
}

def env = System.getenv()

tasks.register("github", GithubDeploymentTask) {
    dependsOn tasks.remapJar

    config {
        token = env.get("GITHUB_TOKEN")
        repository = env.get("GITHUB_REPOSITORY")
    }

    release {
        title = "[$project.minecraft_version] $project.name $project.version"
        tag = buildUtils.latestTag()
    }

    assets.add(tasks.remapJar.archiveFile.get())
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archives_base_name

            artifact remapJar
            artifact remapSourcesJar

            pom {
                name = 'MPlugins'
                description = 'Integration of plugins4j into the fabric mod loader. '
            }
        }
    }

    buildUtils.setupPublishRepository(repositories, props)
}